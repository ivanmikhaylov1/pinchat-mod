plugins {
    id 'fabric-loom' version '1.13-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven { url 'https://maven.fabricmc.net/' }
    maven { url "https://api.modrinth.com/maven" }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.isxander.dev/releases" }
}

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modCompileOnly "maven.modrinth:malilib:0.26.3"
    modCompileOnly "maven.modrinth:cloth-config:20.0.149+fabric"
    modCompileOnly "com.terraformersmc:modmenu:11.0.3"
    modCompileOnly "dev.isxander:yet-another-config-lib:3.8.1+1.21.11-fabric"
    
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
    
    testmodImplementation sourceSets.main.output
    testmodImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    testmodImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    testmodImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

loom {
    runs {
        testClient {
            client()
            name "Test Client"
            source sourceSets.testmod
            
            vmArgs "-Dfabric.log.level=DEBUG"
            property "fabric.log.level", "DEBUG"
        }
    }
}

tasks.named('build') {
    dependsOn 'test'
}
